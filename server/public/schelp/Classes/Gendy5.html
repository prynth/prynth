<html><head><title>Gendy5</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='./../prettify.js' type='text/javascript'></script>
<script src='./../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider CLASSES (extension)</div>
<div id='categories'><a href='./../Browse.html#UGens>Generators>Stochastic'>UGens>Generators>Stochastic</a></div>
<h1>Gendy5</h1>
<div id='summary'>Dynamic stochastic synthesis generator</div>
</div>
<div class='subheader'>
<div id='filename'>Source: /usr/local/share/SuperCollider/Extensions/SC3plugins/BhobUGens/classes/<a href='file:///usr/local/share/SuperCollider/Extensions/SC3plugins/BhobUGens/classes/bhobGens.sc'>bhobGens.sc</a></div><div id='superclasses'>Inherits from: <a href="../Classes/Gendy4.html">Gendy4</a> : <a href="../Classes/UGen.html">UGen</a> : <a href="../Classes/AbstractFunction.html">AbstractFunction</a> : <a href="../Classes/Object.html">Object</a></div>
<div id='related'>See also: <a href="./../Classes/Gendy1.html">Gendy1</a>, <a href="./../Classes/Gendy2.html">Gendy2</a>, <a href="./../Classes/Gendy3.html">Gendy3</a>, <a href="./../Classes/Gendy4.html">Gendy4</a></div>
</div>
<div id='toc'>
<ul class='toc'><li class='toc1'><a href='#description'>Description</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#classmethods'>Class methods</a></li>
<ul class='toc'><li class='toc3'><a href='#*ar'>ar</a>  <a href='#*kr'>kr</a> </li>
<li class='toc2'><a href='#Inherited class methods'>Inherited class methods</a></li>
</ul><li class='toc1'><a href='#instancemethods'>Instance methods</a></li>
<ul class='toc'><li class='toc2'><a href='#Inherited instance methods'>Inherited instance methods</a></li>
</ul><li class='toc1'><a href='#examples'>Examples</a></li>
<ul class='toc'></ul></ul></div><h2><a class='anchor' name='description'>Description</a></h2>

<p>This is a non-interpolating version of Nick Collins's <a href="./../Classes/Gendy1.html">Gendy1</a> UGen. My idea was to use it as a kind of sample and hold jitter UGen. It works quite well in that context, and is not a bad sound generator in its own right.<div class='warning'><span class='warninglabel'>WARNING:</span> if you have lots of CPs and you have fast frequencies, the CPU cost goes up a lot because a new CP move happens every sample!</div><h2><a class='anchor' name='classmethods'>Class Methods</a></h2>
<h3 class='cmethodname'><span class='methprefix'>*</span><a name='*ar' href='./../Overviews/Methods.html#ar'>ar</a> (<span class='argstr'>ampdist: 1</span>, <span class='argstr'>durdist: 1</span>, <span class='argstr'>adparam: 1</span>, <span class='argstr'>ddparam: 1</span>, <span class='argstr'>minfreq: 440</span>, <span class='argstr'>maxfreq: 660</span>, <span class='argstr'>ampscale: 0.5</span>, <span class='argstr'>durscale: 0.5</span>, <span class='argstr'>initCPs: 12</span>, <span class='argstr'>knum</span>, <span class='argstr'>mul: 1</span>, <span class='argstr'>add: 0</span>)</h3>
<div class='supmethod'>From superclass: <a href='./../Classes/Gendy4.html'>Gendy4</a></div>
<h3 class='cmethodname'><span class='methprefix'>*</span><a name='*kr' href='./../Overviews/Methods.html#kr'>kr</a> (<span class='argstr'>ampdist: 1</span>, <span class='argstr'>durdist: 1</span>, <span class='argstr'>adparam: 1</span>, <span class='argstr'>ddparam: 1</span>, <span class='argstr'>minfreq: 20</span>, <span class='argstr'>maxfreq: 1000</span>, <span class='argstr'>ampscale: 0.5</span>, <span class='argstr'>durscale: 0.5</span>, <span class='argstr'>initCPs: 12</span>, <span class='argstr'>knum</span>, <span class='argstr'>mul: 1</span>, <span class='argstr'>add: 0</span>)</h3>
<div class='supmethod'>From superclass: <a href='./../Classes/Gendy4.html'>Gendy4</a></div>
<div class='method'>
<p>see <a href="./../Classes/Gendy1.html#*ar">Gendy1: *ar</a> for explanation of parameters.</div><h3><a class='anchor' name='Inherited class methods'>Inherited class methods</a></h3>
<div id='inheritedclassmets'></div><h2><a class='anchor' name='instancemethods'>Instance Methods</a></h2>
<h3><a class='anchor' name='Inherited instance methods'>Inherited instance methods</a></h3>
<div id='inheritedinstmets'></div><h2><a class='anchor' name='examples'>Examples</a></h2>
<pre class='code prettyprint lang-sc'>//defaults
{Pan2.ar(Gendy5.ar)}.play

//sorry Xenakis!!!
(
var scale, buffer;
scale = FloatArray[0, 2, 4, 6, 7, 9, 11];
buffer = Buffer.alloc(s, scale.size,1, {|b| b.setnMsg(0, scale) });

play({
    var mix;
    mix =
    SinOsc.ar(
        (
            DegreeToKey.kr(
                buffer.bufnum,
                Gendy5.kr(minfreq:1, maxfreq:8, ampscale:0.1, durscale:0.1, initCPs:3, mul:7.5, add:7.5),
                12,
                1,
                60
            )
            + LFNoise1.kr([3,3], 0.04)
        ).midicps,
        0).cubed * 0.1
    + RLPFD.ar(LFPulse.ar([48,55].midicps, 0.15),
        Gendy4.kr(2, 2, 1, 1, 0.1, 0.2, 0.1, 0.1, 2, mul:10, add:72).midicps, 0.6, 0.2);
    CombN.ar(mix, 0.31, 0.31, 2, 0.2, mix)
})
)

//wandering bass/ powerline
{Pan2.ar(Gendy5.ar(1,1,1.0,1.0,30,100,0.3,0.05,5))}.play

//play me
{Pan2.ar(RLPF.ar(Gendy5.ar(2,3,minfreq:20, maxfreq:MouseX.kr(100,1000), durscale:0.0, initCPs:40), 500,0.3, 0.2), 0.0)}.play

//scream! - careful with your ears for this one!
(
{
var mx, my;

mx= MouseX.kr(220,440);
my= MouseY.kr(0.0,1.0);

Pan2.ar(Gendy5.ar(2,3,1,1,minfreq:mx, maxfreq:8*mx, ampscale:my, durscale:my, initCPs:7, mul:0.3), 0.0)}.play
)


//1 CP = random noise effect
{Pan2.ar(Gendy5.ar(initCPs:1))}.play

//2 CPs = suudenly an oscillator (though a fast modulating one here)
{Pan2.ar(Gendy5.ar(initCPs:2))}.play


//used as an LFO
(
{Pan2.ar(SinOsc.ar(Gendy5.kr(2,4,SinOsc.kr(0.1,0,0.49,0.51),SinOsc.kr(0.13,0,0.49,0.51), 3.4,3.5, SinOsc.kr(0.17,0,0.49,0.51), SinOsc.kr(0.19,0,0.49,0.51),10,10,50, 350), 0, 0.3), 0.0)}.play
)

DegreeToKey

//wasp
{Pan2.ar(Gendy5.ar(0, 0, SinOsc.kr(0.1, 0, 0.1, 0.9),1.0, 50,1000, 1,0.005, 12, 12, 0.2), 0.0)}.play


//modulate distributions
//change of pitch as distributions change the duration structure and spectrum
{Pan2.ar(Gendy5.ar(MouseX.kr(0,7),MouseY.kr(0,7),mul:0.2), 0.0)}.play


//modulate num of CPs
{Pan2.ar(Gendy5.ar(knum:MouseX.kr(1,13),mul:0.2), 0.0)}.play


//Gendy into Gendy...with cartoon side effects
{Pan2.ar(Gendy5.ar(maxfreq:Gendy5.kr(5,4,0.3, 0.7, 0.1, MouseY.kr(0.1,10), 1.0, 1.0, 5,5, 500, 600), knum:MouseX.kr(1,13),mul:0.2), 0.0)}.play


//use SINUS to track any oscillator and take CP positions from it, use adparam and ddparam as the inputs to sample
{Pan2.ar(Gendy5.ar(6,6,LFPulse.kr(100, 0, 0.4, 1.0), SinOsc.kr(30, 0, 0.5),mul:0.2), 0.0)}.play


//try out near the corners especially
{Pan2.ar(Gendy5.ar(6,6,LFPulse.kr(MouseX.kr(0,200), 0, 0.4, 1.0), SinOsc.kr(MouseY.kr(0,200), 0, 0.5),mul:0.2), 0.0)}.play


//texture
(
{
Mix.fill(10,{
var freq;

freq= rrand(130,160.3);
Pan2.ar(SinOsc.ar(Gendy5.ar(6.rand,6.rand,SinOsc.kr(0.1,0,0.49,0.51),SinOsc.kr(0.13,0,0.49,0.51),freq ,freq, SinOsc.kr(0.17,0,0.49,0.51), SinOsc.kr(0.19,0,0.49,0.51), 12, 12, 200, 400), 0, 0.1), 1.0.rand2)
});
}.play
)

//wahhhhhhhh- try durscale 1.0 and 0.0 too
(
{Pan2.ar(
CombN.ar(
Resonz.ar(
Gendy5.ar(2,3,minfreq:1, maxfreq:MouseX.kr(10,700), durscale:0.1, initCPs:10),
MouseY.kr(50,1000), 0.1)
,0.1,0.1,5, 0.6
)
, 0.0)}.play
)


//overkill
(
{
var n;
n=10;

Mix.fill(n,{
var freq, numcps;

freq= rrand(130,160.3);
numcps= rrand(2,20);
Pan2.ar(Gendy5.ar(6.rand,6.rand,1.0.rand,1.0.rand,freq ,freq, 1.0.rand, 1.0.rand, numcps, SinOsc.kr(exprand(0.02,0.2), 0, numcps/2, numcps/2), 0.5/(n.sqrt)), 1.0.rand2)
});
}.play
)





//another traffic moment
(
{
var n;
n=10;

Resonz.ar(
Mix.fill(n,{
var freq, numcps;

freq= rrand(50,560.3);
numcps= rrand(2,20);
Pan2.ar(Gendy5.ar(6.rand,6.rand,1.0.rand,1.0.rand,freq ,freq, 1.0.rand, 1.0.rand, numcps, SinOsc.kr(exprand(0.02,0.2), 0, numcps/2, numcps/2), 0.5/(n.sqrt)), 1.0.rand2)
})
,MouseX.kr(100,2000), MouseY.kr(0.01,1.0))
;
}.play
)




(
{
var n;
n=15;

Resonz.ar(
Mix.fill(n,{
var freq, numcps;

freq= rrand(330,460.3);
numcps= rrand(2,20);
Pan2.ar(Gendy5.ar(6.rand,6.rand,1.0.rand,1.0.rand,freq,MouseX.kr(freq,2*freq), 1.0.rand, 1.0.rand, numcps, SinOsc.kr(exprand(0.02,0.2), 0, numcps/2, numcps/2), 0.5/(n.sqrt)), 1.0.rand2)
})
,MouseX.kr(100,2000), MouseY.kr(0.01,1.0)
)

}.play;
)</pre>

<p><div class='doclink'>source: <a href='file:///usr/local/share/SuperCollider/Extensions/SC3plugins/BhobUGens/HelpSource/Classes/Gendy5.schelp'>/usr/local/share/SuperCollider/Extensions/SC3plugins/BhobUGens/HelpSource/Classes/Gendy5.schelp</a><br>link::Classes/Gendy5::<br>sc version: 3.8dev</div></div></body></html>