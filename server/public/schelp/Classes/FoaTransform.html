<html><head><title>FoaTransform</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='./../prettify.js' type='text/javascript'></script>
<script src='./../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider CLASSES (extension)</div>
<div id='categories'><a href='./../Browse.html#Libraries>Ambisonic Toolkit>FOA>Transforming'>Libraries>Ambisonic Toolkit>FOA>Transforming</a>, <a href='./../Browse.html#UGens>Multichannel>Ambisonics'>UGens>Multichannel>Ambisonics</a></div>
<h1>FoaTransform</h1>
<div id='summary'>First Order Ambisonic (FOA) transformer</div>
</div>
<div class='subheader'>
<div id='filename'>Source: /usr/local/share/SuperCollider/Extensions/SC3plugins/ATK/Classes/<a href='file:///usr/local/share/SuperCollider/Extensions/SC3plugins/ATK/Classes/ATK.sc'>ATK.sc</a></div><div id='superclasses'>Inherits from: <a href="../Classes/FoaUGen.html">FoaUGen</a> : <a href="../Classes/Object.html">Object</a></div>
<div id='related'>See also: <a href="./../Classes/FoaXform.html">FoaXform</a></div>
</div>
<div id='toc'>
<ul class='toc'><li class='toc1'><a href='#description'>Description</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#classmethods'>Class methods</a></li>
<ul class='toc'><li class='toc3'><a href='#*ar'>ar</a> </li>
<li class='toc2'><a href='#Inherited class methods'>Inherited class methods</a></li>
</ul><li class='toc1'><a href='#instancemethods'>Instance methods</a></li>
<ul class='toc'><li class='toc2'><a href='#Inherited instance methods'>Inherited instance methods</a></li>
</ul><li class='toc1'><a href='#examples'>Examples</a></li>
<ul class='toc'><li class='toc2'><a href='#Choose a decoder'>Choose a decoder</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Rotate (soundfile)'>Rotate (soundfile)</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#DominateX (soundfile)'>DominateX (soundfile)</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Focus'>Focus</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Push and RTT'>Push and RTT</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Push and RTT with Spread Encoder (soundfile)'>Push and RTT with Spread Encoder (soundfile)</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#X-axis: FocusX, ZoomX & PushX'>X-axis: FocusX, ZoomX &amp; PushX</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Y-axis: Balance and Asymmetry'>Y-axis: Balance and Asymmetry</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Proximity'>Proximity</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#NFC (soundfile)'>NFC (soundfile)</a></li>
<ul class='toc'></ul></ul></ul></div><h2><a class='anchor' name='description'>Description</a></h2>

<p>Transforms (spatial domain filter) a first order ambisonic signal (B-format). <a href="./../Classes/FoaTransform.html">FoaTransform</a> applies dynamic transforms, for static transforms see <a href="./../Classes/FoaXform.html">FoaXform</a>.<div class='note'><span class='notelabel'>NOTE:</span> <a href="./../Classes/FoaTransform.html">FoaTransform</a> is a convenience wrapper around the various transformer UGens.</div><h2><a class='anchor' name='classmethods'>Class Methods</a></h2>
<h3 class='cmethodname'><span class='methprefix'>*</span><a name='*ar' href='./../Overviews/Methods.html#ar'>ar</a> (<span class='argstr'>in</span>, <span class='argstr'>kind</span> <span class='argstr'>... args</span>)</h3>
<div class='method'>
<p><h4>Arguments:</h4>
<table class='arguments'>
<tr><td class='argumentname'>in<td class='argumentdesc'>
<p>The input signal, an array: [in0, in1, ... inN]<tr><td class='argumentname'>kind<td class='argumentdesc'>
<p>The kind of transform to apply. <a class='anchor' name='transforms'>&nbsp;</a><dl>
<dt><code class='code prettyprint lang-sc'>'rtt'        rotAngle = 0, tilAngle = 0, tumAngle = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaRTT.html">FoaRTT</a><dt><code class='code prettyprint lang-sc'>'mirror'    theta = 0, phi = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaMirror.html">FoaMirror</a><dt><code class='code prettyprint lang-sc'>'directO'    angle = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaDirectO.html">FoaDirectO</a><dt><code class='code prettyprint lang-sc'>'direct'    angle = 0, theta = 0, phi = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaDirect.html">FoaDirect</a><dt><code class='code prettyprint lang-sc'>'dominate'    gain = 0, theta = 0, phi = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaDominate.html">FoaDominate</a><dt><code class='code prettyprint lang-sc'>'zoom'        angle = 0, theta = 0, phi = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaZoom.html">FoaZoom</a><dt><code class='code prettyprint lang-sc'>'focus'    angle = 0, theta = 0, phi = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaFocus.html">FoaFocus</a><dt><code class='code prettyprint lang-sc'>'push'        angle = 0, theta = 0, phi = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaPush.html">FoaPush</a><dt><code class='code prettyprint lang-sc'>'press'    angle = 0, theta = 0, phi = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaPress.html">FoaPress</a><dt><code class='code prettyprint lang-sc'>'asymmetry'    angle = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaAsymmetry.html">FoaAsymmetry</a><dt><code class='code prettyprint lang-sc'>'balance'    angle = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaBalance.html">FoaBalance</a><dt><code class='code prettyprint lang-sc'>'nfc'        distance = 1, mul = 1, add = 0</code><dd><a href="./../Classes/FoaNFC.html">FoaNFC</a><dt><code class='code prettyprint lang-sc'>'proximity'    distance = 1, mul = 1, add = 0</code><dd><a href="./../Classes/FoaProximity.html">FoaProximity</a></dl>
<div class='note'><span class='notelabel'>NOTE:</span> <strong>Axial transforms - except as noted, all take args of angle = 0, mul = 1, add = 0 as defaults</strong><dl>
<dt><code class='code prettyprint lang-sc'>'rotate'</code><dd><a href="./../Classes/FoaRotate.html">FoaRotate</a><dt><code class='code prettyprint lang-sc'>'tilt'</code><dd><a href="./../Classes/FoaTilt.html">FoaTilt</a><dt><code class='code prettyprint lang-sc'>'tumble'</code><dd><a href="./../Classes/FoaTumble.html">FoaTumble</a><dt><code class='code prettyprint lang-sc'>'directX'</code><dd><a href="./../Classes/FoaDirectX.html">FoaDirectX</a><dt><code class='code prettyprint lang-sc'>'directY'</code><dd><a href="./../Classes/FoaDirectY.html">FoaDirectY</a><dt><code class='code prettyprint lang-sc'>'directZ'</code><dd><a href="./../Classes/FoaDirectZ.html">FoaDirectZ</a><dt><code class='code prettyprint lang-sc'>'dominateX'    gain = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaDominateX.html">FoaDominateX</a><dt><code class='code prettyprint lang-sc'>'dominateY'    gain = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaDominateY.html">FoaDominateY</a><dt><code class='code prettyprint lang-sc'>'dominateZ'    gain = 0, mul = 1, add = 0</code><dd><a href="./../Classes/FoaDominateZ.html">FoaDominateZ</a><dt><code class='code prettyprint lang-sc'>'zoomX'</code><dd><a href="./../Classes/FoaZoomX.html">FoaZoomX</a><dt><code class='code prettyprint lang-sc'>'zoomY'</code><dd><a href="./../Classes/FoaZoomY.html">FoaZoomY</a><dt><code class='code prettyprint lang-sc'>'zoomZ'</code><dd><a href="./../Classes/FoaZoomZ.html">FoaZoomZ</a><dt><code class='code prettyprint lang-sc'>'focusX'</code><dd><a href="./../Classes/FoaFocusX.html">FoaFocusX</a><dt><code class='code prettyprint lang-sc'>'focusY'</code><dd><a href="./../Classes/FoaFocusY.html">FoaFocusY</a><dt><code class='code prettyprint lang-sc'>'focusZ'</code><dd><a href="./../Classes/FoaFocusZ.html">FoaFocusZ</a><dt><code class='code prettyprint lang-sc'>'pushX'</code><dd><a href="./../Classes/FoaPushX.html">FoaPushX</a><dt><code class='code prettyprint lang-sc'>'pushY'</code><dd><a href="./../Classes/FoaPushY.html">FoaPushY</a><dt><code class='code prettyprint lang-sc'>'pushZ'</code><dd><a href="./../Classes/FoaPushZ.html">FoaPushZ</a><dt><code class='code prettyprint lang-sc'>'pressX'</code><dd><a href="./../Classes/FoaPressX.html">FoaPressX</a><dt><code class='code prettyprint lang-sc'>'pressY'</code><dd><a href="./../Classes/FoaPressY.html">FoaPressY</a><dt><code class='code prettyprint lang-sc'>'pressZ'</code><dd><a href="./../Classes/FoaPressZ.html">FoaPressZ</a></dl>
</div><tr><td class='argumentname'>... args<td class='argumentdesc'>
<p>Arguments (listed above with each 'kind') for the wrapped transformer UGens. Arguments can NOT be passed in through keyword through the FoaTransform wrapper. You can pass values in by keyword if you use the transform UGens directly.</table></div><h3><a class='anchor' name='Inherited class methods'>Inherited class methods</a></h3>
<div id='inheritedclassmets'></div><h2><a class='anchor' name='instancemethods'>Instance Methods</a></h2>
<h3><a class='anchor' name='Inherited instance methods'>Inherited instance methods</a></h3>
<div id='inheritedinstmets'></div><h2><a class='anchor' name='examples'>Examples</a></h2>

<p>The examples below are intended to briefly illustrate some of the first order tranform options made available in the Ambisonic Toolkit. The user is encouraged to carefully review the features of the individual <a href="#transforms">transforms</a> to gain a deeper understanding of the flexibility of these tools.
<p>Available transformers include rotations, mirroring, directivity (spatial low pass fitering), dominance (image warping), and a variety of dominance related transforms.
<p>As the Ambisonic technique is a hierarchal system, numerous options for playback are possible. These include two channel stereo, two channel binaural, pantophonic and full 3D periphonic. With the examples below, we'll take advantage of this by first choosing a suitable decoder with with to audition.<h3><a class='anchor' name='Choose a decoder'>Choose a decoder</a></h3>

<p>Choose a decoder suitable for your system, as illustrated <a href="./../Guides/Intro-to-the-ATK.html#choose_a_decoder">here</a>. You'll end up definining <code class='code prettyprint lang-sc'>~decoder</code> and <code class='code prettyprint lang-sc'>~renderDecode</code> .<div class='note'><span class='notelabel'>NOTE:</span> If you choose a kernel decoder, <a href="./../Classes/FoaDecoderKernel.html">FoaDecoderKernel</a>, be sure to free the kernel after use.</div><h3><a class='anchor' name='Rotate (soundfile)'>Rotate (soundfile)</a></h3>

<p>Rotation is one of the most used soundfield transforms. In this case we'll it to centre the subject of a field recording.
<p>The soundfield is controlled by <a href="./../Classes/MouseX.html">MouseX</a>, which specifies the rotation angle (pi to -pi; left to right of display).
<p>If you haven't already choosen a <code class='code prettyprint lang-sc'>~decoder</code> and defined <code class='code prettyprint lang-sc'>~renderDecode</code>, do so <a href="#choose_a_decoder">now</a>.<div class='note'><span class='notelabel'>NOTE:</span> A UGen type transformer, see <a href="./../Classes/FoaRotate.html">FoaRotate</a> for further details.</div><pre class='code prettyprint lang-sc'>// ------------------------------------------------------------
// rotate transformer
// b-format soundfile read from disk

// choose transformer
~transformer = 'rotate'



// read a whole sound into memory
// remember to free the buffer later!
// (boot the server, if you haven't!)
~sndbuf = Buffer.read(s, Atk.userSoundsDir ++ "/b-format/Courville-Dialogue.wav")
~sndbuf = Buffer.read(s, Atk.userSoundsDir ++ "/b-format/Hodges-Purcell.wav")
~sndbuf = Buffer.read(s, Atk.userSoundsDir ++ "/b-format/Leonard-Orfeo_Trio.wav")


(
{
    var sig;                            // audio signal
    var azim;                           // azimuth control


    // display transformer &amp; decoder
    "Ambisonic transforming via % transformer".format(~transformer).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // azimuth -&gt; hard left     = back
    //            centre        = centre
    //            hard right    = back
    azim = MouseX.kr(pi, -pi);


    // ------------------------------------------------------------
    // test sig
    sig = PlayBuf.ar(~sndbuf.numChannels, ~sndbuf, doneAction:2);   // soundfile

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, ~transformer, azim);


    // ------------------------------------------------------------
    // decode (via ~renderDecode)
    ~renderDecode.value(sig, ~decoder)

}.scope;
)

// free buffer
~sndbuf.free
// ------------------------------------------------------------</pre>
<div class='note'><span class='notelabel'>NOTE:</span> <strong>Soundfile Credits</strong><ul>
<li>D. Courville, "Comparative Surround Recording," Ambisonic Studio | Comparative Surround Recording, 2007. [Online]. Available: <a href="http://www.radio.uqam.ca/ambisonic/comparative_recording.html">http://www.radio.uqam.ca/ambisonic/comparative_recording.html</a> [Accessed: 26-Jul-2011].<li>P. Hodges, "Purcell - Passacaglia (King Arthur)," Sound of Space: ambisonic surround sound. [Online]. Available: <a href="http://soundofspace.com/ambisonic_files/52">http://soundofspace.com/ambisonic_files/52</a> [Accessed: 03-Nov-2011].<li>J. Leonard, "The Orfeo Trio &amp; TetraMic," Sound of Space: ambisonic surround sound. [Online]. Available: <a href="http://soundofspace.com/ambisonic_files/41">http://soundofspace.com/ambisonic_files/41</a> [Accessed: 03-Nov-2011].</ul>
</div><h3><a class='anchor' name='DominateX (soundfile)'>DominateX (soundfile)</a></h3>

<p>Dominance specified in gain is a classic Ambisonic production technique. Here we apply gain across the X axis. With these example recordings, we adjust both the stage width and the subject to reverb balance.
<p>The soundfield is controlled by <a href="./../Classes/MouseY.html">MouseY</a>, which specifies the dominance gain (4.5 dB to -4.5 dB; top to bottom of display).
<p>If you haven't already choosen a <code class='code prettyprint lang-sc'>~decoder</code> and defined <code class='code prettyprint lang-sc'>~renderDecode</code>, do so <a href="#choose_a_decoder">now</a>.<div class='note'><span class='notelabel'>NOTE:</span> A UGen type transformer, see <a href="./../Classes/FoaDominateX.html">FoaDominateX</a> for further details.</div><pre class='code prettyprint lang-sc'>// ------------------------------------------------------------
// dominateX transformer
// b-format soundfile read from disk

// choose transformer
~transformer = 'dominateX'



// read a whole sound into memory
// remember to free the buffer later!
// (boot the server, if you haven't!)
~sndbuf = Buffer.read(s, Atk.userSoundsDir ++ "/b-format/Courville-Dialogue.wav")
~sndbuf = Buffer.read(s, Atk.userSoundsDir ++ "/b-format/Hodges-Purcell.wav")
~sndbuf = Buffer.read(s, Atk.userSoundsDir ++ "/b-format/Leonard-Orfeo_Trio.wav")


(
{
    var sig;                            // audio signal
    var gain;                           // gain control


    // display transformer &amp; decoder
    "Ambisonic transforming via % transformer".format(~transformer).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // gain ---&gt; top            = +4.5db for front
    //            bottom        = -4.5db for front
    gain = MouseY.kr(4.5, 4.5.neg);


    // ------------------------------------------------------------
    // test sig
    sig = PlayBuf.ar(~sndbuf.numChannels, ~sndbuf, doneAction:2);   // soundfile

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, ~transformer, gain);


    // ------------------------------------------------------------
    // decode (via ~renderDecode)
    ~renderDecode.value(sig, ~decoder)

}.scope;
)

// free buffer
~sndbuf.free
// ------------------------------------------------------------</pre>
<div class='note'><span class='notelabel'>NOTE:</span> <strong>Soundfile Credits</strong><ul>
<li>D. Courville, "Comparative Surround Recording," Ambisonic Studio | Comparative Surround Recording, 2007. [Online]. Available: <a href="http://www.radio.uqam.ca/ambisonic/comparative_recording.html">http://www.radio.uqam.ca/ambisonic/comparative_recording.html</a> [Accessed: 26-Jul-2011].<li>P. Hodges, "Purcell - Passacaglia (King Arthur)," Sound of Space: ambisonic surround sound. [Online]. Available: <a href="http://soundofspace.com/ambisonic_files/52">http://soundofspace.com/ambisonic_files/52</a> [Accessed: 03-Nov-2011].<li>J. Leonard, "The Orfeo Trio &amp; TetraMic," Sound of Space: ambisonic surround sound. [Online]. Available: <a href="http://soundofspace.com/ambisonic_files/41">http://soundofspace.com/ambisonic_files/41</a> [Accessed: 03-Nov-2011].</ul>
</div><h3><a class='anchor' name='Focus'>Focus</a></h3>

<p>Here we encode four channels of decorrelated and comb filtered <a href="./../Classes/PinkNoise.html">PinkNoise</a> as a decorrelated soundfield, resulting in a maximally diffuse soundfield. <a href="./../Classes/FoaFocus.html">FoaFocus</a> is used to "focus" on various parts of the soundfield. At extremes, it becomes a planewave (infinite distance, in an anechoic environment) arriving from some direction. This technique gives the opportunity to continuously modulate between a directional and a diffuse soundfield.
<p>The soundfield is controlled by <a href="./../Classes/MouseX.html">MouseX</a> and <a href="./../Classes/MouseY.html">MouseY</a>, where <a href="./../Classes/MouseX.html">MouseX</a> specifies the incident azimuth angle (pi to -pi; left to right of display) and <a href="./../Classes/MouseY.html">MouseY</a> the <a href="./../Classes/FoaFocus.html">FoaFocus</a> angle (0 to pi/2; bottom to top of display). With the mouse at the bottom of the display, the soundfield remains decorrelated. Placed at the top of the display, the soundfield becomes directional, and varying left/right position will vary the incident azimuth of the resulting planewave.
<p>If you haven't already choosen a <code class='code prettyprint lang-sc'>~decoder</code> and defined <code class='code prettyprint lang-sc'>~renderDecode</code>, do so <a href="#choose_a_decoder">now</a>.<div class='note'><span class='notelabel'>NOTE:</span> A UGen type transformer, see <a href="./../Classes/FoaFocus.html">FoaFocus</a> for further details.</div><pre class='code prettyprint lang-sc'>// ------------------------------------------------------------
// focus transformer
// decorrelated, comb filtered pink noise source


// define encoder matrix
~encoder = FoaEncoderMatrix.newAtoB

// choose transformer
~transformer = 'focus'

(
{
    var sig;                // audio signal
    var angle, azim;            // angle and azimuth control
    var freq;


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.kind).postln;
    "Ambisonic transforming via % transformer".format(~transformer).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // frequencies
    freq = 220;

    // angle ---&gt; top         = push to plane wave
    //          bottom    = omni-directional
    angle = MouseY.kr(pi/2, 0);

    // azimuth -&gt; hard left     = back
    //          centre     = centre
    //          hard right     = back
    azim = MouseX.kr(pi, -pi);


    // ------------------------------------------------------------
    // test sig
    sig = PinkNoise.ar([1, 1, 1, 1]); // 4 channels decorrelated pink noise

    // ------------------------------------------------------------
    // comb filter
    sig = HPF.ar(sig, freq);
    sig = CombL.ar(sig, freq.reciprocal, freq.reciprocal, mul: 9.neg.dbamp);


    // ------------------------------------------------------------
    // encode
    sig = FoaEncode.ar(sig, ~encoder);

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, ~transformer, angle, azim);



    // ------------------------------------------------------------
    // decode (via ~renderDecode)
    ~renderDecode.value(sig, ~decoder)

}.scope;
)
// ------------------------------------------------------------</pre>
<h3><a class='anchor' name='Push and RTT'>Push and RTT</a></h3>

<p>Here we encode four channels of <a href="./../Classes/Klank.html">Klank</a> resonated <a href="./../Classes/Dust.html">Dust</a> from A-format. <a href="./../Classes/FoaPush.html">FoaPush</a> is used to "push" the soundfield so that it becomes a planewave (infinite distance, in an anechoic environment) arriving from some direction. This technique gives the opportunity to continuously modulate between a directional and a spatially active soundfield. Additionally, <a href="./../Classes/FoaRTT.html">FoaRTT</a> is used to continuously reorient the granular stream so that individual A-format directions don't predominate, and the complete soundfield is filled with activity.
<p>The soundfield is controlled by <a href="./../Classes/MouseX.html">MouseX</a> and <a href="./../Classes/MouseY.html">MouseY</a>, where <a href="./../Classes/MouseX.html">MouseX</a> specifies the incident azimuth angle (pi to -pi; left to right of display) and <a href="./../Classes/MouseY.html">MouseY</a> the <a href="./../Classes/FoaPush.html">FoaPush</a> angle (0 to pi/2; bottom to top of display). With the mouse at the bottom of the display, the soundfield remains decorrelated. Placed at the top of the display, the soundfield becomes directional, and varying left/right position will vary the incident azimuth of the resulting planewave.
<p>If you haven't already choosen a <code class='code prettyprint lang-sc'>~decoder</code> and defined <code class='code prettyprint lang-sc'>~renderDecode</code>, do so <a href="#choose_a_decoder">now</a>.<div class='note'><span class='notelabel'>NOTE:</span> UGen type transformers, see <a href="./../Classes/FoaPush.html">FoaPush</a> and <a href="./../Classes/FoaRTT.html">FoaRTT</a> for further details.</div><pre class='code prettyprint lang-sc'>// ------------------------------------------------------------
// push and rtt transformer
// granular klank stream source


// define encoder matrix
~encoder = FoaEncoderMatrix.newAtoB

// choose transformer
~transformer1 = 'rtt'
~transformer2 = 'push'

(
{
    var sig;                     // audio signal
    var angle, azim;            // angle and azimuth control
    var freq;

    // ir...
    var gain = -18;

    var freqs = [50.0, 7000.0], gains = [-24, 0], rtimes = [0.1, 2.0];
    var frequencies, amplitudes, ringTimes;
    var numModes = 20;
    
    var density = 20;                  // grain/klank density

    var rttFreq = 10 * density;


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.kind).postln;
    "Ambisonic transforming via % transformer".format(~transformer1).postln;
    "Ambisonic transforming via % transformer".format(~transformer2).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;


    // calculate klank args
    frequencies = Array.rand(numModes, freqs.at(0), freqs.at(1)).sort;
    amplitudes = Array.rand(numModes, gains.at(0), gains.at(1)).sort.reverse.dbamp;
    ringTimes = Array.rand(numModes, rtimes.at(0), rtimes.at(1)).sort.reverse;


    // angle ---&gt; top         = push to plane wave
    //          bottom    = omni-directional
    angle = MouseY.kr(pi/2, 0);

    // azimuth -&gt; hard left     = back
    //          centre     = centre
    //          hard right     = back
    azim = MouseX.kr(pi, -pi);


    // ------------------------------------------------------------
    // test sig
    sig = Dust.ar(Array.fill(4, density / 4));

    // ------------------------------------------------------------
    // encode
    sig = FoaEncode.ar(sig, ~encoder);

    // ------------------------------------------------------------
    // transform 1 (rtt)
    sig = FoaTransform.ar(
        sig,
        ~transformer1,
        LFSaw.ar(rttFreq, pi, add: pi),
        LFSaw.ar(rttFreq**(1/3), pi, add: pi),
        LFSaw.ar(rttFreq**(2/3), pi, add: pi)
    );


    // ------------------------------------------------------------
    // Klank
    sig = gain.dbamp * Klank.ar(
        `[ frequencies, amplitudes, ringTimes ],
        sig
    );


    // ------------------------------------------------------------
    // transform 2 (push)
    sig = FoaTransform.ar(sig, ~transformer2, angle, azim);



    // ------------------------------------------------------------
    // decode (via ~renderDecode)
    ~renderDecode.value(sig, ~decoder)

}.scope;
)
// ------------------------------------------------------------</pre>
<h3><a class='anchor' name='Push and RTT with Spread Encoder (soundfile)'>Push and RTT with Spread Encoder (soundfile)</a></h3>

<p>Here we encode the mono component of a stereo soundfile via the <a href="./../Classes/FoaEncoderKernel.html#*newSpread">FoaEncoderKernel: *newSpread</a> encoder. <a href="./../Classes/FoaPush.html">FoaPush</a> is used to "push" the soundfield so that it becomes a planewave (infinite distance, in an anechoic environment) arriving from some direction. This technique gives the opportunity to continuously modulate between a directional and a spatially spread soundfield. Additionally, <a href="./../Classes/FoaRTT.html">FoaRTT</a> is used to continuously reorient the frequency spread soundfield so that individual frequencies are moved throughout the space, and the complete soundfield is constantly in motion.
<p>The soundfield is controlled by <a href="./../Classes/MouseX.html">MouseX</a> and <a href="./../Classes/MouseY.html">MouseY</a>, where <a href="./../Classes/MouseX.html">MouseX</a> specifies the incident azimuth angle (pi to -pi; left to right of display) and <a href="./../Classes/MouseY.html">MouseY</a> the <a href="./../Classes/FoaPush.html">FoaPush</a> angle (0 to pi/2; bottom to top of display). With the mouse at the bottom of the display, the soundfield remains decorrelated. Placed at the top of the display, the soundfield becomes directional, and varying left/right position will vary the incident azimuth of the resulting planewave.
<p>If you haven't already choosen a <code class='code prettyprint lang-sc'>~decoder</code> and defined <code class='code prettyprint lang-sc'>~renderDecode</code>, do so <a href="#choose_a_decoder">now</a>.<div class='note'><span class='notelabel'>NOTE:</span> UGen type transformers, see <a href="./../Classes/FoaPush.html">FoaPush</a>, <a href="./../Classes/FoaRTT.html">FoaRTT</a> and <a href="./../Classes/FoaEncoderKernel.html#*newSpread">FoaEncoderKernel: *newSpread</a> for further details.</div><pre class='code prettyprint lang-sc'>// ------------------------------------------------------------
// push and rtt transformer
// spreader encoder
// stereo soundfile read from disk


// define encoder matrix
~encoder = FoaEncoderKernel.newSpread(0000)
~encoder = FoaEncoderKernel.newSpread(0001)
~encoder = FoaEncoderKernel.newSpread(0006)
~encoder = FoaEncoderKernel.newSpread(0008)
~encoder = FoaEncoderKernel.newSpread(0010)
~encoder = FoaEncoderKernel.newSpread(0012)

// free kernel (when you swap encoders!)
~encoder.free

// inspect
~encoder
~encoder.kind
~encoder.numChannels
~encoder.dirChannels * 180/pi


// read a whole sound into memory
// remember to free the buffer later!
// (boot the server, if you haven't!)
~sndbuf = Buffer.read(s, Atk.userSoundsDir ++ "/stereo/The_City_Waites-The_Downfall.wav")
~sndbuf = Buffer.read(s, Atk.userSoundsDir ++ "/stereo/The_City_Waites-An_Old.wav")
~sndbuf = Buffer.read(s, Atk.userSoundsDir ++ "/stereo/Aurora_Surgit-Lux_Aeterna.wav")
~sndbuf = Buffer.read(s, Atk.userSoundsDir ++ "/stereo/Aurora_Surgit-Dies_Irae.wav")

// free buffer (when you swap buffers!)
~sndbuf.free


(
{
    var sig;                            // audio signal
    var angle, azim;                    // angle and azimuth control


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.kind).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // angle ---&gt; top           = push to plane wave
    //            bottom        = omni-directional
    angle = MouseY.kr(pi/2, 0);

    // azimuth -&gt; hard left     = back
    //            centre        = centre
    //            hard right    = back
    azim = MouseX.kr(pi, -pi);

    // ------------------------------------------------------------
    // test sig
    sig = PlayBuf.ar(~sndbuf.numChannels, ~sndbuf, doneAction:2);   // soundfile
    sig = 0.5 * sig.sum; // to mono


    // ------------------------------------------------------------
    // encode
    sig = FoaEncode.ar(sig, ~encoder);

    // ------------------------------------------------------------
    // transform
    sig = FoaRTT.ar(sig,
        LFNoise2.kr(1.0/5.0, pi),
        LFNoise2.kr(1.0/5.0, pi),
        LFNoise2.kr(1.0/5.0, pi)
    );
    sig = FoaTransform.ar(sig, 'push', angle, azim);



    // ------------------------------------------------------------
    // decode (via ~renderDecode)
    ~renderDecode.value(sig, ~decoder)

}.scope;
)

// free buffer
~sndbuf.free
// free kernel
~encoder.free
// ------------------------------------------------------------</pre>
<h3><a class='anchor' name='X-axis: FocusX, ZoomX & PushX'>X-axis: FocusX, ZoomX &amp; PushX</a></h3>

<p>With this example we encode four channels of comb filtered <a href="./../Classes/Dust.html">Dust</a> as planewaves arriving from the cardinal directions. <a href="./../Classes/FoaFocusX.html">FoaFocusX</a>, <a href="./../Classes/FoaZoomX.html">FoaZoomX</a> and <a href="./../Classes/FoaPushX.html">FoaPushX</a> are used to distort the soundfield. At extremes, encoded planewaves are distorted to arrive from the same direction. This example allows one to get compare these transforms on the x-axis.
<p>The soundfield is controlled by <a href="./../Classes/MouseY.html">MouseY</a>, with <strong>angle</strong> varying between -pi/2 and pi/2. With the mouse in the centre of the display, the soundfield remains unchanged. Placed at the top or bottom of the display, the soundfield is distorted.
<p>If you haven't already choosen a <code class='code prettyprint lang-sc'>~decoder</code> and defined <code class='code prettyprint lang-sc'>~renderDecode</code>, do so <a href="#choose_a_decoder">now</a>.<div class='note'><span class='notelabel'>NOTE:</span> UGen type transformers, see <a href="./../Classes/FoaFocusX.html">FoaFocusX</a>, <a href="./../Classes/FoaZoomX.html">FoaZoomX</a> &amp; <a href="./../Classes/FoaPushX.html">FoaPushX</a> for further details.</div><pre class='code prettyprint lang-sc'>// ------------------------------------------------------------
// x-axis transformers
// comb filtered dust noise source, at cardinal points


// define encoder matricies, for each cardinal point
(
~encoder = [ 
    FoaEncoderMatrix.newDirection,
    FoaEncoderMatrix.newDirection(pi/2),
    FoaEncoderMatrix.newDirection(pi),
    FoaEncoderMatrix.newDirection(pi.neg/2)
]
)

// choose transformer
~transformer = 'focusX'
~transformer = 'zoomX'
~transformer = 'pushX'

(
{
    var sig;                            // audio signal
    var angle;                          // angle control
    var freq;
    var density = 10;                   // grain density


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.at(0).kind).postln;
    "Ambisonic transforming via % transformer".format(~transformer).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // frequencies
    freq = 220 * [ 4, 5, 7, 6 ] / 4;

    // angle ---&gt; top           = pi/2
    //            bottom        = -pi/2
    angle = MouseY.kr(pi/2, pi.neg/2);


    // ------------------------------------------------------------
    // test sig
    sig = Dust.ar(Array.fill(4, density / 4));


    // ------------------------------------------------------------
    // comb filter
    sig = BPF.ar(sig, freq, mul: 18.dbamp);
    sig = CombL.ar(sig, freq.reciprocal, freq.reciprocal, mul: 9.neg.dbamp);


    // ------------------------------------------------------------
    // encode
    sig = Mix.fill(sig.numChannels, { arg i;  FoaEncode.ar(sig.at(i), ~encoder.at(i)) });
    

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, ~transformer, angle);



    // ------------------------------------------------------------
    // decode (via ~renderDecode)
    ~renderDecode.value(sig, ~decoder)

}.scope;
)
// ------------------------------------------------------------</pre>
<h3><a class='anchor' name='Y-axis: Balance and Asymmetry'>Y-axis: Balance and Asymmetry</a></h3>

<p>With with the <a href="#x-axis:_focusx,_zoomx_&_pushx">above example</a> we encode four channels of comb filtered <a href="./../Classes/Dust.html">Dust</a> as planewaves arriving from the cardinal directions. <a href="./../Classes/FoaZoomY.html">FoaZoomY</a> and <a href="./../Classes/FoaAsymmetry.html">FoaAsymmetry</a> are used to distort the soundfield. At extremes, encoded planewaves are distorted to arrive from the same direction. This example allows one to get compare these transforms on the x-axis.
<p>The soundfield is controlled by <a href="./../Classes/MouseX.html">MouseX</a>, with <strong>angle</strong> varying between -pi/2 and pi/2. With the mouse in the centre of the display, the soundfield remains unchanged. Placed at the left or right of the display, the soundfield is distorted.
<p>If you haven't already choosen a <code class='code prettyprint lang-sc'>~decoder</code> and defined <code class='code prettyprint lang-sc'>~renderDecode</code>, do so <a href="#choose_a_decoder">now</a>.<div class='note'><span class='notelabel'>NOTE:</span> UGen type transformers, see <a href="./../Classes/FoaZoomY.html">FoaZoomY</a> and <a href="./../Classes/FoaAsymmetry.html">FoaAsymmetry</a> for further details.</div><pre class='code prettyprint lang-sc'>// ------------------------------------------------------------
// y-axis transformers
// comb filtered dust noise source, at cardinal points


// define encoder matricies, for each cardinal point
(
~encoder = [ 
    FoaEncoderMatrix.newDirection,
    FoaEncoderMatrix.newDirection(pi/2),
    FoaEncoderMatrix.newDirection(pi),
    FoaEncoderMatrix.newDirection(pi.neg/2)
]
)

// choose transformer
~transformer = 'balance'
~transformer = 'asymmetry'

(
{
    var sig;                            // audio signal
    var angle;                          // angle control
    var freq;
    var density = 10;                   // grain density


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.at(0).kind).postln;
    "Ambisonic transforming via % transformer".format(~transformer).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // frequencies
    freq = 220 * [ 4, 5, 7, 6 ] / 4;

    // angle ---&gt; left          = pi/2
    //            right         = -pi/2
    angle = MouseX.kr(pi/2, pi.neg/2);


    // ------------------------------------------------------------
    // test sig
    sig = Dust.ar(Array.fill(4, density / 4));


    // ------------------------------------------------------------
    // comb filter
    sig = BPF.ar(sig, freq, mul: 18.dbamp);
    sig = CombL.ar(sig, freq.reciprocal, freq.reciprocal, mul: 9.neg.dbamp);


    // ------------------------------------------------------------
    // encode
    sig = Mix.fill(sig.numChannels, { arg i;  FoaEncode.ar(sig.at(i), ~encoder.at(i)) });
    

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, ~transformer, angle);



    // ------------------------------------------------------------
    // decode (via ~renderDecode)
    ~renderDecode.value(sig, ~decoder)

}.scope;
)
// ------------------------------------------------------------</pre>
<h3><a class='anchor' name='Proximity'>Proximity</a></h3>

<p><a href="./../Classes/FoaProximity.html">FoaProximity</a> facilitates the introduction of the <a href="http://en.wikipedia.org/wiki/Proximity_effect_(audio)">proximity effect</a> to encoded signals. At extremes, the proximity effect introduces a strong bass boost, as well as phase differences. The <a href="http://en.wikipedia.org/wiki/Proximity_effect_(audio)">proximity effect</a> can be an important contributor to perceptions of nearness.
<p>The soundfield is controlled by <a href="./../Classes/MouseY.html">MouseY</a>, with <strong>distance</strong> varying between 0.05 and 0.5 meter. With the mouse at the bottom of the display, the soundfield receives the strongest effect, contributing to as sense of nearness.<div class='warning'><span class='warninglabel'>WARNING:</span> As <a href="./../Classes/FoaProximity.html">FoaProximity</a> includes a 1st-order integration, signals must be highpass filtered before application. <a href="./../Classes/HPF.html">HPF</a> is usually a suitable choice to control low frequency boost.</div>
<p>If you haven't already choosen a <code class='code prettyprint lang-sc'>~decoder</code> and defined <code class='code prettyprint lang-sc'>~renderDecode</code>, do so <a href="#choose_a_decoder">now</a>.<div class='note'><span class='notelabel'>NOTE:</span> UGen type transformer, see <a href="./../Classes/FoaProximity.html">FoaProximity</a> for further details.</div><pre class='code prettyprint lang-sc'>// ------------------------------------------------------------
// proximity transform
// comb filtered dust noise source, panned across the front


// define encoder matricies
(
~encoder = [ 
    FoaEncoderMatrix.newDirection(pi/6),
    FoaEncoderMatrix.newDirection(pi/12),
    FoaEncoderMatrix.newDirection(pi.neg/12),
    FoaEncoderMatrix.newDirection(pi.neg/6)
]
)

// choose transformer
~transformer = 'proximity'

(
{
    var sig;                            // audio signal
    var dist;                           // distance control
    var freq;
    var density = 10;                   // grain density


    // display encoder and decoder
    "Ambisonic encoding via % encoder".format(~encoder.at(0).kind).postln;
    "Ambisonic transforming via % transformer".format(~transformer).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // frequencies
    freq = 220 * [ 4, 5, 7, 6 ] / 4;

    // dist  ---&gt; top           = 0.5
    //            bottom        = 0.05
    dist = MouseY.kr(0.5, 0.05);


    // ------------------------------------------------------------
    // test sig
    sig = Dust.ar(Array.fill(4, density / 4));


    // ------------------------------------------------------------
    // comb filter
    sig = BPF.ar(sig, freq, mul: 18.dbamp);
    sig = CombL.ar(sig, freq.reciprocal, freq.reciprocal, mul: 9.neg.dbamp);


    // ------------------------------------------------------------
    // encode
    sig = Mix.fill(sig.numChannels, { arg i;  FoaEncode.ar(sig.at(i), ~encoder.at(i)) });
    

    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, ~transformer, dist);



    // ------------------------------------------------------------
    // decode (via ~renderDecode)
    ~renderDecode.value(sig, ~decoder)

}.scope;
)
// ------------------------------------------------------------</pre>
<h3><a class='anchor' name='NFC (soundfile)'>NFC (soundfile)</a></h3>

<p><a href="./../Classes/FoaNFC.html">FoaNFC</a> facilitates the reduction or removal of the <a href="http://en.wikipedia.org/wiki/Proximity_effect_(audio)">proximity effect</a> from encoded signals. The <a href="http://en.wikipedia.org/wiki/Proximity_effect_(audio)">proximity effect</a> can be an important contributor to perceptions of nearness.
<p>The soundfield is controlled by <a href="./../Classes/MouseY.html">MouseY</a>, with <strong>distance</strong> varying between 0.5 and 0.05 meter. With the mouse at the top of the display, the soundfield receives the strongest effect (removal), contributing to as sense of distance.
<p>If you haven't already choosen a <code class='code prettyprint lang-sc'>~decoder</code> and defined <code class='code prettyprint lang-sc'>~renderDecode</code>, do so <a href="#choose_a_decoder">now</a>.<div class='note'><span class='notelabel'>NOTE:</span> UGen type transformer, see <a href="./../Classes/FoaNFC.html">FoaNFC</a> for further details.</div><pre class='code prettyprint lang-sc'>// ------------------------------------------------------------
// NFC transform
// b-format soundfile read from disk




// read a whole sound into memory
// remember to free the buffer later!
// (boot the server, if you haven't!)
~sndbuf = Buffer.read(s, Atk.userSoundsDir ++ "/b-format/Anderson-Nearfield.wav")


// choose transformer
~transformer = 'nfc'


(
{
    var sig;                            // audio signal
    var dist;                           // distance control



    // display encoder and decoder
    "Ambisonic transforming via % transformer".format(~transformer).postln;
    "Ambisonic decoding via % decoder".format(~decoder.kind).postln;

    // dist  ---&gt; top           = 0.05
    //            bottom        = 1.0
    dist = MouseY.kr(0.05, 1.0);


    // ------------------------------------------------------------
    // test sig
    sig = PlayBuf.ar(~sndbuf.numChannels, ~sndbuf, doneAction:2);   // soundfile


    // ------------------------------------------------------------
    // transform
    sig = FoaTransform.ar(sig, ~transformer, dist);


    // ------------------------------------------------------------
    // decode (via ~renderDecode)
    ~renderDecode.value(sig, ~decoder)

}.scope;
)

// free buffer
~sndbuf.free
// ------------------------------------------------------------</pre>
<div class='note'><span class='notelabel'>NOTE:</span> <strong>Soundfile Credits</strong><ul>
<li>Joseph Anderson, "Nearfield source," [unpublished recording]</ul>
</div>
<p><div class='doclink'>source: <a href='file:///usr/local/share/SuperCollider/Extensions/SC3plugins/ATK/HelpSource/Classes/FoaTransform.schelp'>/usr/local/share/SuperCollider/Extensions/SC3plugins/ATK/HelpSource/Classes/FoaTransform.schelp</a><br>link::Classes/FoaTransform::<br>sc version: 3.8dev</div></div></body></html>