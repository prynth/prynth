<html><head><title>NodeProxy roles</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='./../prettify.js' type='text/javascript'></script>
<script src='./../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider REFERENCE</div>
<div id='categories'><a href='./../Browse.html#Libraries>JITLib>NodeProxy'>Libraries>JITLib>NodeProxy</a></div>
<h1>NodeProxy roles</h1>
<div id='summary'>Roles in NodeProxy</div>
</div>
<div class='subheader'>
<div id='related'>See also: <a href="./../Classes/NodeProxy.html">NodeProxy</a>, <a href="./../Classes/Ndef.html">Ndef</a>, <a href="./../Classes/ProxySpace.html">ProxySpace</a></div>
</div>
<div id='toc'>
<ul class='toc'><li class='toc1'><a href='#Existing roles'>Existing roles</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Adding new roles'>Adding new roles</a></li>
<ul class='toc'></ul></ul></div>
<p>Similar to Adverbs (see <a href="./../Guides/J-concepts-in-SC.html">J concepts in SC</a>), roles allow to specify how a source for a <a href="./../Classes/NodeProxy.html">NodeProxy</a> is being used. A role is an association of a <a href="./../Classes/Symbol.html">Symbol</a> and the new proxy source object.
<p>The below examples can equally be used for <a href="./../Classes/Ndef.html">Ndef</a> and in <a href="./../Classes/ProxySpace.html">ProxySpace</a>.<pre class='code prettyprint lang-sc'>// Thus, the following expressions behave in an equivalent way:

a = NodeProxy(s);
a[0] = ...

ProxySpace.push(s);
~a[0] = ...

Ndef(\a, ...)</pre>
<h2><a class='anchor' name='Existing roles'>Existing roles</a></h2>
<dl>
<dt>\set -&gt; event pattern<dd>Set the proxy controls with an event pattern of type <code class='code prettyprint lang-sc'>\set</code>. Old values are kept, only those explicitly provided are overridden.<pre class='code prettyprint lang-sc'>a = NodeProxy(s);
a[0] = { |freq = 440, dt=0.1, rate=2| Ringz.ar(Impulse.ar(rate * [1, 1.2]), freq, dt)*0.1 };
a.play;
(
a[1] = \set -&gt; Pbind(
    \dur, Prand([1, 0.5], inf),
    \freq, Pwhite(200.0, 1000, inf),
    \rate, Pstutter(4, Prand([1, 3, 6, 10], inf)),
    \dt, Pwhite(0.01, 0.1, inf)
)
);
// modify the source in the meanwhile:
a[0] = { |freq = 440, dt=0.1, rate=2| Ringz.ar(Dust.ar(rate * 10.dup), freq, dt)*0.1 };

a.nodeMap.postln; // the values are not set in the node map.
a.clear(3);</pre>
<dt>\pset -&gt; event pattern<dd>set all proxy controls to event data<pre class='code prettyprint lang-sc'>a = NodeProxy(s);
a[0] = { |freq = 440, dt=0.1, rate=2| Ringz.ar(Impulse.ar(rate * [1, 1.2]), freq, dt)*0.1 };
a.play;

(
a[1] = \xset -&gt; Pbind(
    \dur, Prand([1, 0.5], inf),
    \freq, Pwhite(200.0, 1000, inf).round(30),
    \rate, Pstutter(4, Prand([1, 3, 6, 10], inf)),
    \dt, Pwhite(0.01, 0.1, inf) + 1
)
);

a.nodeMap.postln; // the values are set in the node map.</pre>
<dt>\xset -&gt; event pattern<dd>set all proxy controls to event data, using synth crossfade (see <a href="#-xset">-xset</a>).<pre class='code prettyprint lang-sc'>a = NodeProxy(s);
a[0] = { |freq = 440, dt=0.1, rate=2| Ringz.ar(Impulse.ar(rate * [1, 1.2]), freq, dt)*0.1 };
a.play;

(
a[1] = \xset -&gt; Pbind(
    \dur, Prand([1, 0.5], inf),
    \freq, Pwhite(200.0, 1000, inf).round(30),
    \rate, Pstutter(4, Prand([1, 3, 6, 10], inf)),
    \dt, Pwhite(0.01, 0.1, inf) + 1
)
);

a.fadeTime = 2;
// modify the source in the meanwhile:
a[0] = { |freq = 440, dt=0.1, rate=2| Ringz.ar(Dust.ar(rate * 10.dup), freq, dt)*0.1 };

a.clear(3);</pre>
<dt>\setbus -&gt; event pattern<dd>Set the proxy bus with an event pattern of type <code class='code prettyprint lang-sc'>\c_set</code><pre class='code prettyprint lang-sc'>a = NodeProxy(s);
b = NodeProxy(s).play;
b[0] = { SinOsc.ar(a.kr(4)).sum * 0.2 };
(
a[0] = \setbus -&gt; Pbind(
    \dur, Prand([1, 0.5], inf),
    \value, Pfunc { var z = rrand(300, 4000); [300, 400, z, z + 30.rand2] }
)
);
// modify the other source in the meanwhile:
b[0] = { Pulse.ar(a.ar(4)).sum * 0.2 };

a.clear; b.clear;</pre>
<dt>\setsrc -&gt; event pattern<dd>Set the proxy source at the next index with any object, controlled by a pattern. Note that any existing source at the next index (in the example below it is index 1) is overridden by the procedure.<pre class='code prettyprint lang-sc'>a = NodeProxy(s);
a.play;
(
a[0] = \setsrc -&gt; Pbind(
    \dur, Prand([1, 0.5, 2], inf),
    \source, Prand ([
        { SinOsc.ar(SinOsc.ar({5.rand}.dup + 4) * 50 + 400 + 50.rand) * 0.1 },
        { SinOsc.ar(LFSaw.ar({5.rand}.dup + 4) * 50 + 400 + 50.rand) * 0.1},
        { LFSaw.ar(SinOsc.ar({5.rand}.dup + 4) * 50 + 400 + 50.rand) * 0.1 },
        { LFSaw.ar(LFSaw.ar({5.rand}.dup + 4) * 50 + 400 + 50.rand) * 0.1 }
    ], inf)
)
);

a.clear(3);</pre>
<dt>\filter -&gt; function<dd>Filter the audio on the proxy's own bus, using the first argument to pass in the sound. The function is any valid UGen function, which may be control or audio rate. Default controls are wet++index, where <strong>index</strong> is the slot of the proxy (default 0), in the example below, the control is <code class='code prettyprint lang-sc'>\wet1</code>.<pre class='code prettyprint lang-sc'>a = NodeProxy(s);
a[0] = { PinkNoise.ar(0.1.dup) };
a.play;
a[1] = \filter -&gt; { |in| RLPF.ar(in, LFNoise2.kr(1).exprange(300, 1000), 0.1) };
a.set(\wet1, 0.2);
a.clear(3);</pre>
<dt>\filterIn -&gt; function<dd>Like <code class='code prettyprint lang-sc'>\filter</code>, only that the input is controled by the <code class='code prettyprint lang-sc'>\wet</code> control, not the output.<pre class='code prettyprint lang-sc'>a = NodeProxy(s);
a[0] = { PinkNoise.ar(0.1.dup) };
a.play;
a[1] = \filterIn -&gt; { |in| RLPF.ar(in, LFNoise2.kr(1).exprange(300, 1000), 0.1) };
a.set(\wet1, 0.2);
a.clear(3);</pre>
<dt>\mix -&gt; function<dd>Mix in the UGen in the function.<pre class='code prettyprint lang-sc'>a = NodeProxy(s);
a[0] = { PinkNoise.ar(0.1.dup) };
a.play;
a[1] = \mix -&gt; { Dust.ar(30.dup) };
a.set(\mix1, 0.2);
a.clear(3);</pre>
</dl>
<h2><a class='anchor' name='Adding new roles'>Adding new roles</a></h2>

<p>Roles can be added on the fly. They are kept in a dictionary ( <strong>buildMethods</strong> ) in <a href="./../Classes/AbstractPlayControl.html">AbstractPlayControl</a>. A second dictionary ( <strong>proxyControlClasses</strong> ) provides the wrapper class for a given key.
<p>Here is a new role that allows you to set a control rate node proxy with the help of an event pattern. The new values are in a key named \value.<pre class='code prettyprint lang-sc'>// add the new role:
(
AbstractPlayControl.proxyControlClasses.put(\stream, PatternControl);
AbstractPlayControl.buildMethods.put(\stream,
    #{ arg pattern, proxy, channelOffset=0, index;
        Pbindf(
            pattern,
            \type, \bus,
            \id, Pfunc { proxy.group.nodeID },
            \array, Pkey(\value),
            \out, Pfunc { proxy.index }
        ).buildForProxy( proxy, channelOffset, index )
});
)


// test:
a = NodeProxy.control(s);
a.source = \stream -&gt; Pbind(\value, Pseq([1, 2, 3], inf), \dur, 1.5).trace;

b = NodeProxy(s);
b.source = { SinOsc.ar([340, 440] * a.kr) * 0.1 };
b.play;

a.source = \stream -&gt; Pbind(\value, Pseq([1, 2, 3], inf) + Pwhite(0.0, 0.2, inf), \dur, 0.05);
b.source = { SinOsc.ar([5.6, 10.3] ** a.kr + 300) * 0.1 };</pre>

<p><div class='doclink'>source: <a href='file:///usr/local/share/SuperCollider/HelpSource/Reference/NodeProxy_roles.schelp'>/usr/local/share/SuperCollider/HelpSource/Reference/NodeProxy_roles.schelp</a><br>link::Reference/NodeProxy_roles::<br>sc version: 3.8dev</div></div></body></html>