<!doctype html><html lang='en'><head><title>Understanding Streams, Patterns and Events - Part 5 | SuperCollider 3.11.2 Help</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../codemirror.css' type='text/css' />
<link rel='stylesheet' href='./../editor.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta name='viewport' content='width=device-width, initial-scale=1'>
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../lib/jquery.min.js'></script>
<script src='./../lib/codemirror-5.39.2.min.js' type='text/javascript'></script>
<script src='./../lib/codemirror-addon-simple-5.39.2.min.js' type='text/javascript'></script>
<script>
var helpRoot = './..';
var scdoc_title = 'Understanding Streams, Patterns and Events - Part 5';
var scdoc_sc_version = '3.11.2';
</script>
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='qrc:///qtwebchannel/qwebchannel.js' type='text/javascript'></script>
</head>
<body onload='fixTOC()'>
<div id='toc'>
<div id='toctitle'>Understanding Streams, Patterns and Events - Part 5:</div>
<span class='toc_search'>Filter: <input id='toc_search'></span><ul class='toc'><li class='toc1'><a href='#protoEvents'>protoEvents</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#~finish'>~finish</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#The%20pitch%20model'>The pitch model</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Pitch%20model%20Examples'>Pitch model Examples</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#The%20duration%20model'>The duration model</a></li>
<ul class='toc'></ul></ul></div><div id='menubar'></div>
<div class='contents'>
<div class='header'>
<div id='label'>
<span id='folder'>Tutorials</span>
 | <span id='categories'><a href='./../Browse.html#Tutorials'>Tutorials</a>&#8201;&gt;&#8201;<a href='./../Browse.html#Tutorials>Streams-Patterns-Events'>Streams-Patterns-Events</a></span>
</div><h1>Understanding Streams, Patterns and Events - Part 5</h1>
<div id='summary'>Event.default</div>
</div>
<div class='subheader'>
<div id='related'>See also: <a href="./../Tutorials/Streams-Patterns-Events1.html">Understanding Streams, Patterns and Events - Part 1</a>, <a href="./../Tutorials/Streams-Patterns-Events2.html">Understanding Streams, Patterns and Events - Part 2</a>, <a href="./../Tutorials/Streams-Patterns-Events3.html">Understanding Streams, Patterns and Events - Part 3</a>, <a href="./../Tutorials/Streams-Patterns-Events4.html">Understanding Streams, Patterns and Events - Part 4</a>, <a href="./../Tutorials/Streams-Patterns-Events6.html">Understanding Streams, Patterns and Events - Part 6</a>, <a href="./../Tutorials/Streams-Patterns-Events7.html">Understanding Streams, Patterns and Events - Part 7</a></div>
</div>

<p>More about the default Event:<h2><a class='anchor' name='protoEvents'>protoEvents</a></h2>

<p>The protoEvent contains default values for many useful parameters.
<p>The default protoEvent is <code>Event.default</code>. It provides default bindings for duration, envelope, instrument, making a very simple Pattern directly playable:<textarea class='editor'>(
// an endless sequence of middle Cs
Pbind.new.play
)</textarea>

<p>By adding other bindings, you can override the defaults in the protoEvent.<textarea class='editor'>(
// duration 0.25 beats (16th notes)
Pbind( \dur, 0.25 ).play
)</textarea>
<textarea class='editor'>(
// specifying the pitch in terms of midinote
// see also The pitch model below
Pbind(
    \dur, 0.125,
    \legato, 0.2,
    \midinote, Pseq(#[60, 62, 64, 65, 67, 69, 71, 72], inf)
).play
)</textarea>
<h2><a class='anchor' name='~finish'>~finish</a></h2>

<p>Event.default contains a function bound to the Symbol <code>'finish'</code> which is called for each new event generated in order to complete any computations that depend on the other values in the event.<h2><a class='anchor' name='The%20pitch%20model'>The pitch model</a></h2>

<p>Event.default implements a multi level pitch model which allows composition using modal scale degrees, equal division note values, midi note values, or frequencies in Hertz. These different ways of specifying the pitch can all be used interchangeably.
<p>The way this works is due to the default values bound to the Symbols of the pitch model.
<p>The lowest level Symbol in the pitch model is <code>'freq'</code>. The default binding for <code>'freq'</code> is a <a href="./../Classes/Function.html">Function</a> which calculates the frequency by getting the value of <code>'midinote'</code>, adding a transpose value and converting it to Hertz using <code>midicps</code>.<textarea class='editor'>    ~freq = {
        (~midinote.value + ~ctranspose).midicps;
    };</textarea>

<p>If you compose with <code>'freq'</code> directly then this default function is overridden.<textarea class='editor'>(
Pbind(
    \dur, 0.25,
    \freq, Pseq(#[300, 400, 500, 700, 900], inf)
).play;
)</textarea>

<p>Event.default's <code>'finish'</code> function sends the value message to the current binding of <code>'freq'</code> in order to get the value for the frequency and adds a detune value to it which transposes the frequency in Hertz.<textarea class='editor'>(
Pbind(
    \dur, 0.25,
    \detune, -20,
    \freq, Pseq(#[300, 400, 500, 700, 900], inf)
).play
)</textarea>

<p>The next level is <code>'midinote'</code> which is by default bound to this function:<textarea class='editor'>    ~midinote = {
        (~note.value + ~gtranspose + (~octave * divs) + ~root)
                * 12.0 / ~stepsPerOctave;
    };</textarea>

<p>This function gets the value bound to <code>'note'</code> which is a value expressed in some equal temperament, not necessarily 12. It adds a gamut transpose value <code>'gtranspose'</code>, and scales from the number of notes per octave being used into 12 notes per octave MIDI key values. If you compose with <code>'midinote'</code> directly then that will override this function.<textarea class='editor'>(
Pbind(
    \dur, 0.2,
    \midinote, Pseq([ Pshuf(#[60, 61, 62, 63, 64, 65, 66, 67], 3) ], inf)
).play
)</textarea>

<p>Another level higher is <code>'note'</code> which is defined by default by this function:<textarea class='editor'>    ~note = {
        var divs;
        divs = ~stepsPerOctave;
        (~degree + ~mtranspose).degreeToKey(~scale, divs);
    };</textarea>

<p>This function derives the note value from the next higher level variables which specify a pitch from a scale. These variables are defined as follows:<textarea class='editor'>    ~stepsPerOctave = 12.0;</textarea>

<p>The number of equal divisions of an octave for this tuning. The equal temperament defined by this variable is known as the gamut. If you wanted to work in cents for example you could set this to 1200.0.<textarea class='editor'>    ~octave = 5.0;</textarea>

<p>The current octave. Middle C is the lowest note in octave 5.<textarea class='editor'>    ~root = 0.0;</textarea>

<p>The root of the scale given in equal divisions defined by <code>~stepsPerOctave</code>.<textarea class='editor'>    ~scale = #[0, 2, 4, 5, 7, 9, 11]; // diatonic major scale</textarea>

<p>A set of scale pitches given in equal divisions defined by <code>~stepsPerOctave</code>.<textarea class='editor'>    ~degree = 0;</textarea>

<p>A scale degree index into the <code>~scale</code>. 0 is the root and the scale wraps in the manner defined by <code>degreeToKey</code>.<textarea class='editor'>    ~mtranspose = 0;</textarea>

<p>A modal transposition value that is added to the scale degree.<textarea class='editor'>    ~gtranspose = 0;</textarea>

<p>A gamut transposition value that is added to the gamut pitch.<textarea class='editor'>    ~ctranspose = 0;</textarea>

<p>A chromatic transposition value expressed in semitones.<h2><a class='anchor' name='Pitch%20model%20Examples'>Pitch model Examples</a></h2>
<textarea class='editor'>(
// a simple scale degree sequence
Pbind(
        // -7 is 8ve below, -3 is a 4th below,
        // 0 is root, 2 is 3rd above, 4 is 5th above, 7 is 8ve above.
    \degree, Pseq([ Pshuf(#[-7,-3,0,2,4,7], 4), Pseq([0,1,2,3,4,5,6,7]) ], inf),
    \dur, 0.15
).play
)


(
// change the octave
Pbind(
    \dur, 0.15,
    \octave, 4,
    \degree, Pseq([ Pshuf(#[-7,-3,0,2,4,7], 4), Pseq([0,1,2,3,4,5,6,7]) ], inf)
).play
)


(
// change the scale
Pbind(
    \dur, 0.15,
    \scale, [0, 2, 3, 5, 7, 8, 10],
    \degree, Pseq([ Pshuf(#[-7,-3,0,2,4,7], 4), Pseq([0,1,2,3,4,5,6,7]) ], inf)
).play
)


(
// modal transposition
var notes;
notes = Pseq([ Pshuf(#[-7,-3,0,2,4,7], 4), Pseq([0,1,2,3,4,5,6,7]) ], 1);
Pseq([
    Pbind(
        \dur, 0.15,
        \mtranspose, 0,
        \degree, notes
    ),
    Pbind(
        \dur, 0.15,
        \mtranspose, 1,
        \degree, notes
    ),
    Pbind(
        \dur, 0.15,
        \mtranspose, 2,
        \degree, notes
    )
], inf).play
)


(
// chromatic transposition
var notes;
notes = Pseq([ Pshuf(#[-7,-3,0,2,4,7], 4), Pseq([0,1,2,3,4,5,6,7]) ], 1);
Pseq([
    Pbind(
        \dur, 0.15,
        \ctranspose, 0,
        \degree, notes
    ),
    Pbind(
        \dur, 0.15,
        \ctranspose, 3,
        \degree, notes
    ),
    Pbind(
        \dur, 0.15,
        \ctranspose, -3,
        \degree, notes
    )
], inf).play
)


(
// frequency detuning
var notes;
notes = Pseq([ Pshuf(#[-7,-3,0,2,4,7], 4), Pseq([0,1,2,3,4,5,6,7]) ], 1);
Pseq([
    Pbind(
        \dur, 0.15,
        \detune, 0,
        \degree, notes
    ),
    Pbind(
        \dur, 0.15,
        \detune, 20,
        \degree, notes
    ),
    Pbind(
        \dur, 0.15,
        \detune, 40,
        \degree, notes
    )
], inf).play
)


(
// chords. If an Array of pitches is returned by a Stream for pitch, then a chord
// will be played.
Pbind(
    \dur, 0.15,
    \degree, Pseq([
        Pshuf(#[-7,-3,0,2,4,7], 4)+[0,4],
        Pseq( [0,1,2,3,4,5,6,7] )+[0,2]
    ], inf)
).play
)


(
// composing in non 12 equal temperaments. 72 tone equal temp.
Pbind(
    \stepsPerOctave, 72,
    \note, Pseq([
            // 1/1, 7/6, 3/2, 7/4, 9/8
        Pseq([ [0,16,42,58,84], Pseq([ 0, 16, 42, 58, 72, 84 ], 2), [0,16,42,58,84] ], 1),
            // 1/1, 6/5, 3/2, 9/5, 9/8
        Pseq([ [0,19,42,61,84], Pseq([ 0, 19, 42, 61, 72, 84 ], 2), [0,19,42,61,84] ], 1),
            // 1/1, 5/4, 3/2, 15/8, 9/8
        Pseq([ [0,23,42,65,84], Pseq([ 0, 23, 42, 65, 72, 84 ], 2), [0,23,42,65,84] ], 1),
            // 1/1, 9/7, 3/2, 27/14, 9/8
        Pseq([ [0,26,42,68,84], Pseq([ 0, 26, 42, 68, 72, 84 ], 2), [0,26,42,68,84] ], 1)
        ], inf),
    \dur, Pseq([ 1.2, Pseq([0.15], 12), 1.2], inf)
).play
)</textarea>
<h2><a class='anchor' name='The%20duration%20model'>The duration model</a></h2>

<p>Duration is expressed in beats and is bound to the <code>'dur'</code> symbol. The sustain time of a note can be expressed directly in beats or by using a legato value which is multiplied by the note duration to get the sustain time.<textarea class='editor'>(
// changing duration
Pbind(
    \dur, Pseq([ Pgeom(0.05, 1.1, 24), Pgeom(0.5, 0.909, 24) ], inf),
    \midinote, Pseq(#[60, 58], inf)
).play
)


(
// changing legato value
Pbind(
    \dur, 0.2,
    \legato, Pseq([ Pseries(0.05, 0.05, 40), Pseries(2.05, -0.05, 40) ], inf),
    \midinote, Pseq(#[48, 51, 55, 58, 60, 58, 55, 51], inf)
).play
)</textarea>

<p>To go to the next file: <a href="./../Tutorials/Streams-Patterns-Events6.html">Understanding Streams, Patterns and Events - Part 6</a><div class='doclink'>helpfile source: <a href='file:///usr/share/SuperCollider/HelpSource/Tutorials/Streams-Patterns-Events5.schelp'>/usr/share/SuperCollider/HelpSource/Tutorials/Streams-Patterns-Events5.schelp</a><br>link::Tutorials/Streams-Patterns-Events5::<br></div></div><script src='./../editor.js' type='text/javascript'></script>
</body></html>