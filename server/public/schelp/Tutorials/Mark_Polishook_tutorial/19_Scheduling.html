<html><head><title>19_Scheduling</title>
<link rel='stylesheet' href='./../../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../../scdoc.js' type='text/javascript'></script>
<script src='./../../docmap.js' type='text/javascript'></script>
<script src='./../../prettify.js' type='text/javascript'></script>
<script src='./../../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./../..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider TUTORIALS/MARK_POLISHOOK_TUTORIAL</div>
<div id='categories'><a href='./../../Browse.html#Tutorials>Mark_Polishook_tutorial'>Tutorials>Mark_Polishook_tutorial</a></div>
<h1>19_Scheduling</h1>
<div id='summary'>Mark Polishook tutorial</div>
</div>
<div class='subheader'>
<div id='related'>See also: <a href="./../../Tutorials/Mark_Polishook_tutorial/00_Introductory_tutorial.html">00_Introductory tutorial</a></div>
</div>
<div id='toc'>
<ul class='toc'><li class='toc1'><a href='#Routines and clocks'>Routines and clocks</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Scheduling routines'>Scheduling routines</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Clocks and the convenience of .play'>Clocks and the convenience of .play</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Scheduling synths with routines'>Scheduling synths with routines</a></li>
<ul class='toc'></ul></ul></div><h2><a class='anchor' name='Routines and clocks'>Routines and clocks</a></h2>

<p>Use clocks to create automated, algorithmic scheduling. Among the things that clocks "play" are routines, tasks, and patterns.
<p>To see how a clock "plays" a routine, first examine how a function works in a routine.
<p>The first argument (and usually the only argument) to a routine is a function.<pre class='code prettyprint lang-sc'>// template for a routine
Routine({ ".... code within curly braces is a function .... "});</pre>

<p>A .yield message to an expression in a function (in a routine) returns a value.<pre class='code prettyprint lang-sc'>r = Routine({ "hello, world".yield.postln });

// to evaluate a routine, send a .next message
// it will "hand over" the value of the expression to which the .yield message is attached
r.next;</pre>

<p>Evaluate (again)<pre class='code prettyprint lang-sc'>r.next;</pre>

<p>The routine above returns nil when its evaluated a second time. This is because once a routine "yields" and if there's no additional code after the .yield message, the routine is finished, over, and done - unless it receives a reset message. Then it can start over again.<pre class='code prettyprint lang-sc'>r.next;        // returns nil
r.reset;    // reset the routine
r.next;        // it works!</pre>

<p>////////////////////////////////////////////////////////////////////////////////////////////////////<pre class='code prettyprint lang-sc'>(
r = Routine({
    "hello, world".yield;
    "what a world".yield;
    "i am a world".yield;
});
)</pre>

<p>The first three .next messages return a string. The fourth .next message returns nil.<pre class='code prettyprint lang-sc'>r.next;    // returns a string
r.next;    // returns a string
r.next;    // returns a string
r.next;    // returns nil</pre>

<p>Reset the routine.<pre class='code prettyprint lang-sc'>r.reset;

r.next;
r.next;
r.next;
r.next;</pre>

<p>////////////////////////////////////////////////////////////////////////////////////////////////////
<p>Use a .do message in a routine to make a loop.<pre class='code prettyprint lang-sc'>(
r = Routine({

    // setup code
    var array;
    array = [ "hello, world", "what a world", "i am a world" ];

    // the loop
    3.do({ array.choose.yield })

});
)</pre>

<p>Evaluate the routine one more time than the loop in the routine allows.<pre class='code prettyprint lang-sc'>4.do({ r.next.postln });</pre>

<p>The routine returned three strings followed by nil.<h2><a class='anchor' name='Scheduling routines'>Scheduling routines</a></h2>

<p>Rewrite the routine so that it includes a .wait message.<pre class='code prettyprint lang-sc'>(
r = Routine({

    var array;
    array = [ "hello, world", "what a world", "i am a world" ];

    3.do({
        1.wait;         // pause for 1 second
        array.choose.postln;
    })

});
)</pre>

<p>Then "play" the routine, eg, send it a .play message.<pre class='code prettyprint lang-sc'>r.play;</pre>

<p>Append a .reset message to the routine so that it can start over.<pre class='code prettyprint lang-sc'>r.reset.play;</pre>
<h2><a class='anchor' name='Clocks and the convenience of .play'>Clocks and the convenience of .play</a></h2>

<p>When a routine receives a .play message, control (of the routine) is redirected to a clock. The clock uses the receiver of the .wait message as a unit of time to schedule ("play") the routine.
<p>SuperCollider has three clocks, each of which has a help file.<pre class='code prettyprint lang-sc'>SystemClock        // the most accurate
AppClock        // for use with GUIs
TempoClock        // to schedule in beats</pre>

<p>The .play message is a convenience that allows one to write<pre class='code prettyprint lang-sc'>r.reset.play;        // reset the routine before playing it</pre>

<p>instead of<pre class='code prettyprint lang-sc'>SystemClock.play(r)</pre>
<h2><a class='anchor' name='Scheduling synths with routines'>Scheduling synths with routines</a></h2>

<p>Enclose synths within routines. It's often the case that the synthdef used by the synth in routines should have an envelope with a doneAction parameter set to 2 (to deallocate the memory needed for the synth after its envelope has finished playing).<pre class='code prettyprint lang-sc'>(
// DEFINE A SYNTHDEF
SynthDef("fm2", {
    arg bus = 0, freq = 440, carPartial = 1, modPartial = 1, index = 3, mul = 0.2, ts = 1;

    // index values usually are between 0 and 24
    // carPartial :: modPartial =&gt; car/mod ratio

    var mod;
    var car;

    mod = SinOsc.ar(
        freq * modPartial,
        0,
        freq * index * LFNoise1.kr(5.reciprocal).abs
    );

    car = SinOsc.ar(
        (freq * carPartial) + mod,
        0,
        mul
    );

    Out.ar(
        bus,
        car * EnvGen.kr(Env.sine(1), doneAction: 2, timeScale: ts)
    )
}).add;
)

(
// DEFINE A ROUTINE
r = Routine({

    12.do({
        Synth(
            "fm2",
            [
                \bus, 2.rand, \freq, 400.0.rrand(1200),
                \carPartial, 0.5.rrand(2), \ts, 0.5.rrand(11)
            ]
        );
        s.queryAllNodes;
        "".postln.postln.postln.postln.postln;
        2.wait;
    })
});
)

// PLAY THE ROUTINE
r.reset.play;</pre>

<p>////////////////////////////////////////////////////////////////////////////////////////////////////
<p>Process synths spawned in a routine through effects that run outside of the routine.<pre class='code prettyprint lang-sc'>(
// DEFINE A SYNTHDEF
SynthDef("echoplex", {
    ReplaceOut.ar(
        0,
        CombN.ar(
            In.ar(0, 1),
            0.35,
            [Rand(0.05, 0.3), Rand(0.05, 0.3)],
            // generate random values every time a synth is created
            7,
            0.5
        )
    )
}).add;

// DEFINE GROUPS TO CONTROL ORDER-OF-EXECUTION
// attach a ~source group to the head of the rootnode and
// an ~effects group to the tail of the rootenode
~source = Group.head(s);
~effect = Group.tail(s);

// DEFINE A ROUTINE
r = Routine({

    // loop is the same as inf.do, eg, create an infinite loop that runs forever
    loop({
        Synth.head(    // attach the synth to the head of the ~source group
            ~source,
            "fm2",
            [
                \outbus, 0, \freq, 400.0.rrand(1200), \modPartial, 0.3.rrand(2.0),
                \carPartial, 0.5.rrand(11), \ts, 0.1.rrand(0.2)]
        );
        s.queryAllNodes;
        2.wait;
    })
});

// TURN ON EFFECTS
Synth.head(~effect, "echoplex");
Synth.tail(~effect, "echoplex");
)
// PLAY THE ROUTINE
r.reset.play;</pre>

<p>////////////////////////////////////////////////////////////////////////////////////////////////////
<p>go to <a href="./../../Tutorials/Mark_Polishook_tutorial/20_Debugging.html">20_Debugging</a><div class='doclink'>source: <a href='file:///usr/local/share/SuperCollider/HelpSource/Tutorials/Mark_Polishook_tutorial/19_Scheduling.schelp'>/usr/local/share/SuperCollider/HelpSource/Tutorials/Mark_Polishook_tutorial/19_Scheduling.schelp</a><br>link::Tutorials/Mark_Polishook_tutorial/19_Scheduling::<br>sc version: 3.8dev</div></div></body></html>