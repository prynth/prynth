<!doctype html><html lang='en'><head><title>jitlib_fading | SuperCollider 3.9.3 Help</title>
<link rel='stylesheet' href='./../../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script>
var helpRoot = './../..';
var scdoc_title = 'jitlib_fading';
var scdoc_sc_version = '3.9.3';
</script>
<script src='./../../scdoc.js' type='text/javascript'></script>
<script src='./../../docmap.js' type='text/javascript'></script>
<script src='./../../prettify.js' type='text/javascript'></script>
<script src='./../../lang-sc.js' type='text/javascript'></script>
</head>
<body onload='fixTOC();prettyPrint()'>
<div id='toc'>
<div id='toctitle'>jitlib_fading:</div>
<span class='toc_search'>Filter: <input id='toc_search'></span><ul class='toc'><li class='toc1'><a href='#a)%20automatic%20fade%20envelope%20generation'>a) automatic fade envelope generation</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#b)%20automatic%20free%20instead%20of%20crossfade'>b) automatic free instead of crossfade</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#c)%20custom%20fade%20envelope'>c) custom fade envelope</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#d)%20SynthDef%20name%20assignment'>d) SynthDef name assignment</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#e)%20own%20free%20responsibility'>e) own free responsibility</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#f)%20own%20output%20responsibility'>f) own output responsibility</a></li>
<ul class='toc'></ul></ul></div><div class='contents'>
<div id='menubar'></div>
<div class='header'>
<div id='label'>
<span id='folder'>Tutorials/JITLib</span>
 | <span id='categories'><a href='./../../Browse.html#Libraries'>Libraries</a>&#8201;&gt;&#8201;<a href='./../../Browse.html#Libraries>JITLib'>JITLib</a>&#8201;&gt;&#8201;<a href='./../../Browse.html#Libraries>JITLib>Tutorials'>Tutorials</a> | <a href='./../../Browse.html#Tutorials'>Tutorials</a>&#8201;&gt;&#8201;<a href='./../../Browse.html#Tutorials>JITLib'>JITLib</a></span>
</div><h1>jitlib_fading</h1>
<div id='summary'>Fade envelope generation and crossfading</div>
</div>
<div class='subheader'>
<div id='related'>See also: <a href="./../../Overviews/JITLib.html">JITLib</a></div>
</div>

<p><a href="./../../Classes/NodeProxy.html">NodeProxy</a> ( <a href="./../../Classes/ProxySynthDef.html">ProxySynthDef</a> ) looks for inner envelopes in your definition function to find out whether a fade envelope is needed or not. In case there is no other inner possibility of freeing the synth, either<ul>
<li><a href="#a)_automatic_fade_envelope_generation">a)</a> a fade envelope is created (audio / control rate output)<li><a href="#b)_automatic_free_instead_of_crossfade">b)</a> the synth is freed directly with no fading (scalar output or doneAction 1)<li><a href="#c)_custom_fade_envelope">c)</a> if you provide a gate arg and a doneAction 2 to your ugenGraph function, this is supposed to be a fade envelope for the synth<li><a href="#d)_synthdef_name_assignment">d)</a> if a synthdef name is used, case <a href="#c)_custom_fade_envelope">c)</a> is supposed</ul>

<p>... so in most cases, there is not much to worry about, just these two points ar important, if one wants to use a self releasing synth or a different out ugen:<ul>
<li><a href="#e)_own_free_responsibility">e)</a> own responsibility: if the function creates a ugengraph that can be freed by trigger or other things, it waits for this action instead of the node proxy freeing the synth.<li><a href="#f)_own_output_responsibility">f)</a> own out channel with 'out' arg: the control ugen with the name 'out' is set to the output channel number of the proxy.</ul>
<pre class='code prettyprint lang-sc'>p = ProxySpace.push(s.boot);

~out.play;

// note that you can use this functionality also when using ProxySynthDef directly:

d = ProxySynthDef("test", { arg freq=440; SinOsc.ar(freq) }).send(s);
s.sendMsg("/s_new", "test", 1980, 1, 1, \freq, 340);
s.sendMsg("/n_set", 1980, \freq, 240);
s.sendMsg("/n_set", 1980, \fadeTime, 4);
s.sendMsg("/n_set", 1980, \gate, 0);</pre>
<h2><a class='anchor' name='a)%20automatic%20fade%20envelope%20generation'>a) automatic fade envelope generation</a></h2>
<pre class='code prettyprint lang-sc'>// no inner envelope and audio / control rate output
(
~out = { PinkNoise.ar([1,1]*0.1) };
)

(
~kout = { PinkNoise.kr([1,1]*0.1) };
)</pre>
<h2><a class='anchor' name='b)%20automatic%20free%20instead%20of%20crossfade'>b) automatic free instead of crossfade</a></h2>
<pre class='code prettyprint lang-sc'>// inner envelope that cannot free the synth, the synth is freed when a new
// function is assigned.
(
~out = { arg t_trig; EnvGen.kr(Env.asr, t_trig) * PinkNoise.ar([1,1]) };
)
~out.group.set(\t_trig, 1);

(
~out = { arg t_trig; EnvGen.kr(Env.asr, t_trig) * SinOsc.ar([1,1]*400) };
)
~out.group.set(\t_trig, 1);


// for a scalar output also no fade env is created, but the synth is freed (without fading)
(
~out = { Out.ar(0, SinOsc.ar(Rand(440,550),0,0.2)) };
)</pre>
<h2><a class='anchor' name='c)%20custom%20fade%20envelope'>c) custom fade envelope</a></h2>
<pre class='code prettyprint lang-sc'>// when a gate arg is provided, and the env can free the synth, this envelope
// is supposed to be the fade envelope for the synth: no extra fade env is created.
(
~out = { arg gate=1; EnvGen.kr(Env.asr, gate, doneAction: Done.freeSelf) * 0.2 * SinOsc.ar([1,1]*Rand(440,550)) };
)</pre>
<h2><a class='anchor' name='d)%20SynthDef%20name%20assignment'>d) SynthDef name assignment</a></h2>
<pre class='code prettyprint lang-sc'>// if a symbol is used as input, the defname of a def on the server is supposed
// to represent a SynthDef that has a gate, an out input and can free itself.
(
~out = \default;
)

// this is the minimal requirement arguments for such a use (similar to Pbind)
(
SynthDef("test", { arg gate=1, out;
    Out.ar(out, Formant.ar(300, 200, 10) * EnvGen.kr(Env.asr, gate, doneAction: Done.freeSelf))
}).send(s);
)

// you can also provide a fadeTime arg, whic is set by the proxy:
(
SynthDef("test", { arg gate=1, out, fadeTime=1;
    Out.ar(out,
        Formant.ar(Rand(20,40), 600, 10, 0.2)
        * EnvGen.kr(Env.asr(fadeTime,1,fadeTime), gate, doneAction: Done.freeSelf)
    )
}).send(s);
)

~out = \test;
~out.fadeTime = 3;</pre>
<div class='note'><span class='notelabel'>NOTE:</span> the <strong>number of channels</strong> is your own responsibility when using symbols, as a symbol carries no channel information! (in all other cases the number of channels is wrapped or expanded to fit the proxy)</div><pre class='code prettyprint lang-sc'>// if the synthdef has a fixed duration envelope, there is a FAILURE /n_set Node not found message.
// with no further significance
(
SynthDef("test", { arg gate=1, out;
    Out.ar(out,
        Formant.ar(Rand(20,40), 600, 10, 0.6)
        * EnvGen.kr(Env.perc, gate, doneAction: Done.freeSelf)
    )
}).send(s);
)

~out = \test;</pre>
<h2><a class='anchor' name='e)%20own%20free%20responsibility'>e) own free responsibility</a></h2>
<pre class='code prettyprint lang-sc'>//inner envelope that can free the synth, no extra fade env is created:
(
~out = { arg t_trig; EnvGen.kr(Env.asr, t_trig, doneAction: Done.freeSelf) * PinkNoise.ar([1,1]) };
)
~out.group.set(\t_trig, 1); //end it

~out.send; //start a new synth
~out.group.set(\t_trig, 1); //end it again


// if there is a ugen that can free the synth, no extra fade env is created either,
// but it supposes the synth frees itself, so if a new function is assigned it does
// not get freed.
(
~out = { arg t_trig;
    FreeSelf.kr(t_trig);
    PinkNoise.ar([1,1]*0.3);
};
)
~out.group.set(\t_trig, 1);</pre>
<h2><a class='anchor' name='f)%20own%20output%20responsibility'>f) own output responsibility</a></h2>
<pre class='code prettyprint lang-sc'>// the arg name 'out' can be used to output through the right channel.
// of course with this one can get problems due to a wrong number of channels
// or deliberate hacks.

//left speaker
(
~out = { arg out;
    OffsetOut.ar(out, Impulse.ar(10,0,0.1))
}
)

//both speakers
(
~out = { arg out;
    OffsetOut.ar(out, Impulse.ar([10, 10],0,0.1))
}
)

//this plays out into another adjacent bus: this is a possible source of confusion.
(
~out = { arg out;
    OffsetOut.ar(out, Impulse.ar([10, 10, 10],0,0.1))
}
)</pre>

<p><div class='doclink'>helpfile source: <a href='file:///private/var/folders/qf/zrk81q7s6ygdbbgwm1_h5gc80000gn/T/AppTranslocation/8BAA739F-B195-446C-A6B4-C103296EBF70/d/SuperCollider.app/Contents/Resources/HelpSource/Tutorials/JITLib/jitlib_fading.schelp'>/private/var/folders/qf/zrk81q7s6ygdbbgwm1_h5gc80000gn/T/AppTranslocation/8BAA739F-B195-446C-A6B4-C103296EBF70/d/SuperCollider.app/Contents/Resources/HelpSource/Tutorials/JITLib/jitlib_fading.schelp</a><br>link::Tutorials/JITLib/jitlib_fading::<br></div></div></body></html>